


buildscript {
    repositories {
        jcenter()
    }
    dependencies {
        classpath 'com.github.jengelman.gradle.plugins:shadow:6.1.0'
    }
}

apply plugin: 'com.github.johnrengelman.shadow'
apply plugin: 'java'
apply plugin: 'maven-publish'
apply plugin: 'maven'
apply plugin: 'signing'
apply plugin: 'jacoco'


compileJava.options.encoding = 'UTF-8'

group = "com.github.zuinnote"
archivesBaseName = "hadoopoffice-hiveserde"
version = "1.5.0"



shadowJar {
   relocate 'org.apache.commons.compress', 'hadoopoffice.shade.org.apache.commons.compress'
     manifest {
        attributes 'Implementation-Title': 'ZuInnoTe - Hadoop Office Analytics Library - Serde', 'Implementation-Version': version
    }

    classifier = ''

    exclude 'META-INF/*.RSA', 'META-INF/*.SF','META-INF/*.DSA' // remove signature digests of other libraries
}


repositories {
    mavenCentral()
    mavenLocal()
    // workaround for certain dependencies of hive-exec
    maven {
    	url "http://conjars.org/repo"
    }

}


jacocoTestReport {
    reports {
        xml.enabled true
        csv.enabled true
    }
}




configurations {
	provided
	integrationTestCompile.extendsFrom testCompile
    	integrationTestRuntime.extendsFrom testRuntime
}

eclipse {

  classpath {
    plusConfigurations += [ configurations.provided ]
  }
}


test {
    testLogging.showStandardStreams = true

    testLogging {
        events "passed", "skipped", "failed"
        exceptionFormat "full"
  }
    useJUnitPlatform()
}

sourceSets {
    main.compileClasspath += configurations.provided
    test.compileClasspath += configurations.provided
    test.runtimeClasspath += configurations.provided
    integrationTest {
        java {
            compileClasspath += main.output + test.output + configurations.provided
            runtimeClasspath += main.output + test.output + configurations.provided
            srcDir file('src/integration-test/java')
        }
        resources.srcDir file('src/integration-test/resources')
    }


}



dependencies {

   // hadoop lib for driver
    provided("org.apache.hadoop:hadoop-client:3.3.0")

       // hadoopoffice library
   compile("com.github.zuinnote:hadoopoffice-fileformat:1.5.0")
   // bouncy castle (for signing etc.)
   // for signing and verification of signatures
    provided("org.bouncycastle:bcprov-ext-jdk15on:1.68")
    provided("org.bouncycastle:bcpkix-jdk15on:1.68")
    provided("org.apache.santuario:xmlsec:2.2.1")
    // hive serde API
   provided("org.apache.hive:hive-serde:3.1.2")
   provided("org.apache.hive:hive-exec:3.1.2")
   // log4j2
   provided("org.apache.logging.log4j:log4j-api:2.14.0")
   // test
    testRuntime("org.apache.hive:hive-serde:3.1.2")
    testCompile group: 'org.junit.jupiter', name: 'junit-jupiter-api', version: '5.7.0'
	testRuntime group: 'org.junit.jupiter', name: 'junit-jupiter-engine', version: '5.7.0'
}


publishing {
    publications {
        maven(MavenPublication) {
            groupId 'com.github.zuinnote'
            artifactId 'hadoopoffice-hiveserde'
            version '1.5.0'

            from components.java
        }
    }
}

javadoc.classpath += configurations.provided

task hadoopOfficeJavaDocs(type: Jar) {
  classifier = 'javadoc'
  from javadoc
}


task sourcesJar(type: Jar) {
    classifier = 'sources'
    from sourceSets.main.allSource
}

artifacts {
    archives shadowJar, hadoopOfficeJavaDocs, sourcesJar
}
