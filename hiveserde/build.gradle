


buildscript {
    repositories {
        jcenter()
    }
    dependencies {
        classpath 'com.github.jengelman.gradle.plugins:shadow:4.0.0'
    }
}

apply plugin: 'com.github.johnrengelman.shadow'
apply plugin: 'java'
apply plugin: 'maven-publish'
apply plugin: 'maven'
apply plugin: 'signing'
apply plugin: 'jacoco'


sourceCompatibility = 1.8
compileJava.options.encoding = 'UTF-8'

group = "com.github.zuinnote"
archivesBaseName = "hadoopoffice-hiveserde"
version = "1.3.9"



shadowJar {
   relocate 'org.apache.commons.compress', 'hadoopoffice.shade.org.apache.commons.compress'
     manifest {
        attributes 'Implementation-Title': 'ZuInnoTe - Hadoop Office Analytics Library - Serde', 'Implementation-Version': version
    }

    classifier = ''

    exclude 'META-INF/*.RSA', 'META-INF/*.SF','META-INF/*.DSA' // remove signature digests of other libraries
}


repositories {
    mavenCentral()
    mavenLocal()
    // workaround for certain dependencies of hive-exec
    maven {
    	url "http://conjars.org/repo"
    }

}


jacocoTestReport {
    reports {
        xml.enabled true
        csv.enabled true
    }
}




configurations {
	provided
	integrationTestCompile.extendsFrom testCompile
    	integrationTestRuntime.extendsFrom testRuntime
}

eclipse {

  classpath {
    plusConfigurations += [ configurations.provided ]
  }
}


test {
    testLogging.showStandardStreams = true

    testLogging {
        events "passed", "skipped", "failed"
        exceptionFormat "full"
  }
    useJUnitPlatform()
}

sourceSets {
    main.compileClasspath += configurations.provided
    test.compileClasspath += configurations.provided
    test.runtimeClasspath += configurations.provided
    integrationTest {
        java {
            compileClasspath += main.output + test.output + configurations.provided
            runtimeClasspath += main.output + test.output + configurations.provided
            srcDir file('src/integration-test/java')
        }
        resources.srcDir file('src/integration-test/resources')
    }
    
    
}



dependencies {

   // hadoop lib for driver
    provided("org.apache.hadoop:hadoop-client:2.7.0")
    
       // hadoopoffice library
   compile("com.github.zuinnote:hadoopoffice-fileformat:1.3.9")
   // bouncy castle (for signing etc.)
   // for signing and verification of signatures
    provided("org.bouncycastle:bcprov-ext-jdk15on:1.62")
    provided("org.bouncycastle:bcpkix-jdk15on:1.62")
    provided("org.apache.santuario:xmlsec:2.1.2")
    // hive serde API
   provided("org.apache.hive:hive-serde:2.3.6")
   provided("org.apache.hive:hive-exec:2.3.6")
   // log4j2
   provided("org.apache.logging.log4j:log4j-api:2.13.1")
   // test
    testRuntime("org.apache.hive:hive-serde:2.3.6")
    testCompile group: 'org.junit.jupiter', name: 'junit-jupiter-api', version: '5.6.1'
	testRuntime group: 'org.junit.jupiter', name: 'junit-jupiter-engine', version: '5.6.1'
}


publishing {
    publications {
        maven(MavenPublication) {
            groupId 'com.github.zuinnote'
            artifactId 'hadoopoffice-hiveserde'
            version '1.3.9'

            from components.java
        }
    }
}

javadoc.classpath += configurations.provided

task hadoopOfficeJavaDocs(type: Jar) {
  classifier = 'javadoc'
  from javadoc
}


task sourcesJar(type: Jar) {
    classifier = 'sources'
    from sourceSets.main.allSource
}

artifacts {
    archives shadowJar, hadoopOfficeJavaDocs, sourcesJar
}