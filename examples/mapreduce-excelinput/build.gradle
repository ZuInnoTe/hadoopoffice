apply plugin: 'java'
apply plugin: 'eclipse'
apply plugin: 'application'
apply plugin: 'jacoco'
 apply plugin: 'org.junit.platform.gradle.plugin'

mainClassName = "org.zuinnote.hadoop.office.example.driver.Excel2CSVDriver"
compileJava.options.encoding = 'UTF-8'
sourceCompatibility = 1.8
version = '1.0'
jar {
    manifest {
        attributes 'Implementation-Title': 'Example - MR job (ExcelInput) for reading Excel files using hadoopoffice', 'Implementation-Version': version
    }

    baseName = 'example-ho-mr-excelinput'
    version = '0.1.0'
   // note this builds one fat jar and it is not recommended for production use - just for illustration purpose
 doFirst {
        from { configurations.compile.collect { it.isDirectory() ? it : zipTree(it) } }
    }
    exclude 'META-INF/*.RSA', 'META-INF/*.SF','META-INF/*.DSA' // remove signature digests of other libraries
}

repositories {
    mavenCentral()
    mavenLocal()
}

jacocoTestReport {
    reports {
        xml.enabled true
        csv.enabled true
    }
}


configurations {
	provided
	testProvided
	testIntegrationCompile.extendsFrom testCompile
    testIntegrationRuntime.extendsFrom testRuntime
}

eclipse {

  classpath {
    plusConfigurations += [ configurations.provided ]
    plusConfigurations += [ configurations.testProvided ]
    plusConfigurations += [ configurations.testIntegrationCompile ]
    plusConfigurations += [ configurations.testIntegrationRuntime ]
  }
}

javadoc.classpath += configurations.provided


// Integrate JaCoCo for jUnit5
project.afterEvaluate {
    def junitPlatformTestTask = project.tasks.getByName('junitPlatformTest')

    // configure jacoco to analyze the junitPlatformTest task
    jacoco {
        applyTo junitPlatformTestTask
    }

    // create junit platform jacoco task
    project.task(type: JacocoReport, "junitPlatformJacocoReport",
            {
                sourceDirectories = files("./src/main")
                classDirectories = files("$buildDir/classes/main")
                executionData junitPlatformTestTask
            })
}


sourceSets {
    main.compileClasspath += configurations.provided
    test.compileClasspath += configurations.provided
    test.runtimeClasspath += configurations.provided
     testIntegration {
        java {
            compileClasspath += main.output + test.output + configurations.provided
            runtimeClasspath += main.output + test.output + configurations.provided
            srcDir file('src/integration-test/java')
        }
        resources.srcDir file('src/integration-test/resources')
    }
}



dependencies {
   // hadoop lib for driver
     provided("org.apache.hadoop:hadoop-client:2.7.0")
     // hadoopoffice library
   compile("com.github.zuinnote:hadoopoffice-fileformat:1.0.4")

       testCompile group: 'org.junit.jupiter', name: 'junit-jupiter-api', version: '5.0.2'
 
   testRuntime group: 'org.junit.jupiter', name: 'junit-jupiter-engine', version: '5.0.2'
    testIntegrationRuntime group: 'org.junit.jupiter', name: 'junit-jupiter-engine', version: '5.0.2'
    testIntegrationRuntime group: 'org.junit.platform', name: 'junit-platform-console', version: '1.0.2'
     testIntegrationCompile group: 'org.apache.hadoop', name: 'hadoop-minicluster', version: '2.7.0'
}




uploadArchives {
    repositories {
       flatDir {
           dirs 'repos'
       }
    }
}

task testUnit(type: JavaExec, dependsOn: jar) {
    group 'Verification'
    description 'Runs the unit tests.'

    classpath = sourceSets.test.runtimeClasspath
    systemProperties['jar.path'] = jar.archivePath

    main 'org.junit.platform.console.ConsoleLauncher'
    args = ['--scan-classpath', sourceSets.testIntegration.output.classesDirs.asPath,
            '--reports-dir', "${buildDir}/test-results/testIntegration"
    ]
}

task testIntegration(type: JavaExec, dependsOn: jar) {
    group 'Verification'
    description 'Runs the integration tests.'
 
    classpath = sourceSets.testIntegration.runtimeClasspath
    systemProperties['jar.path'] = jar.archivePath

    main 'org.junit.platform.console.ConsoleLauncher'
    args = ['--scan-classpath', sourceSets.testIntegration.output.classesDirs.asPath,
            '--reports-dir', "${buildDir}/test-results/integration-test"
    ]
}


check.dependsOn testIntegration
testIntegration.mustRunAfter testUnit

afterEvaluate {
    jacoco {
        applyTo testUnit
        applyTo testIntegration
    }
}