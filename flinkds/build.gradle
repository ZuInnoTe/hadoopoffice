apply plugin: 'java'
apply plugin: 'maven-publish'
apply plugin: 'maven'
apply plugin: 'signing'
apply plugin: 'jacoco'


sourceCompatibility = 1.8
compileJava.options.encoding = 'UTF-8'

group = "com.github.zuinnote"
archivesBaseName = "hadoopoffice-flinkds"
version = "1.3.3"


jar {
    manifest {
        attributes 'Implementation-Title': 'ZuInnoTe - Hadoop Office Analytics Library -  Flink DS (Data Source/Data Sink)', 'Implementation-Version': version
    }

    baseName = 'hadoopoffice-flinkds'
}


repositories {
    mavenCentral()
    mavenLocal()


}


jacocoTestReport {
    reports {
        xml.enabled true
        csv.enabled true
    }
}


configurations {
	provided
	integrationTestCompile.extendsFrom testCompile
    	integrationTestRuntime.extendsFrom testRuntime
}

eclipse {

  classpath {
    plusConfigurations += [ configurations.provided ]
  }
}

sourceSets {
    main.compileClasspath += configurations.provided
    test.compileClasspath += configurations.provided
    test.runtimeClasspath += configurations.provided
    integrationTest {
        java {
            compileClasspath += main.output + test.output + configurations.provided
            runtimeClasspath += main.output + test.output + configurations.provided
            srcDir file('src/integration-test/java')
        }
        resources.srcDir file('src/integration-test/resources')
    }
}

dependencies {
   // hadoop lib for driver
     provided("org.apache.hadoop:hadoop-client:2.7.0")
    // hadoopoffice library
   compile("com.github.zuinnote:hadoopoffice-fileformat:1.3.3")
    // flink api
   provided("org.apache.flink:flink-java:1.7.0")
   // log4j2
   provided("org.apache.logging.log4j:log4j-api:2.11.2")
   // testing
     testCompile group: 'org.junit.jupiter', name: 'junit-jupiter-api', version: '5.4.2'
 	 testRuntime group: 'org.junit.jupiter', name: 'junit-jupiter-engine', version: '5.4.2'
}

publishing {
    publications {
        maven(MavenPublication) {
            groupId 'com.github.zuinnote'
            artifactId 'hadoopoffice-flinkds'
            version '1.3.3'

            from components.java
        }
    }
}




test {
    testLogging.showStandardStreams = true
    useJUnitPlatform()
}

javadoc.classpath += configurations.provided

task hadoopOfficeJavaDocs(type: Jar) {
  classifier = 'javadoc'
  from javadoc
}


task sourcesJar(type: Jar) {
    classifier = 'sources'
    from sourceSets.main.allSource
}

artifacts {
    archives hadoopOfficeJavaDocs, sourcesJar
}