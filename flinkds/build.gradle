apply plugin: 'java'
apply plugin: 'maven-publish'
apply plugin: 'signing'
apply plugin: 'jacoco'


sourceCompatibility = 1.8
compileJava.options.encoding = 'UTF-8'

group = "com.github.zuinnote"
archivesBaseName = "hadoopoffice-flinkds"
version = "1.6.2"


jar {
    manifest {
        attributes 'Implementation-Title': 'ZuInnoTe - Hadoop Office Analytics Library -  Flink DS (Data Source/Data Sink)', 'Implementation-Version': version
    }

    baseName = 'hadoopoffice-flinkds'
}


repositories {
    mavenCentral()
    mavenLocal()


}


jacocoTestReport {
    reports {
        xml.enabled true
        csv.enabled true
    }
}


configurations {
	provided
	integrationTestCompile.extendsFrom testCompile
    	integrationTestRuntime.extendsFrom testRuntime
}

eclipse {

  classpath {
    plusConfigurations += [ configurations.provided ]
  }
}

sourceSets {
    main.compileClasspath += configurations.provided
    test.compileClasspath += configurations.provided
    test.runtimeClasspath += configurations.provided
    integrationTest {
        java {
            compileClasspath += main.output + test.output + configurations.provided
            runtimeClasspath += main.output + test.output + configurations.provided
            srcDir file('src/integration-test/java')
        }
        resources.srcDir file('src/integration-test/resources')
    }
}

dependencies {
     // hadoopoffice library
   implementation("com.github.zuinnote:hadoopoffice-fileformat:1.6.2") exclude group: 'org.apache.logging.log4j' // exclude log4j2 until fixed
   // hadoop lib for driver
     compileOnly("org.apache.hadoop:hadoop-client:3.3.0")
    // flink api
   compileOnly("org.apache.flink:flink-java:1.14.0")
   // log4j2
   compileOnly("org.apache.logging.log4j:log4j-api:2.17.0")
   // testing
    // hadoop lib for driver
     testImplementation("org.apache.hadoop:hadoop-client:3.3.0")
    // flink api
   testImplementation("org.apache.flink:flink-java:1.14.0")
   // log4j2
   testImplementation("org.apache.logging.log4j:log4j-api:2.17.0")
     testImplementation group: 'org.junit.jupiter', name: 'junit-jupiter-api', version: '5.8.1'
 	 testRuntimeOnly group: 'org.junit.jupiter', name: 'junit-jupiter-engine', version: '5.8.1'
}

publishing {
    publications {
        maven(MavenPublication) {
            groupId 'com.github.zuinnote'
            artifactId 'hadoopoffice-flinkds'
            version '1.6.2'

            from components.java
        }
    }
}




test {
    systemProperty "java.awt.headless", "true"
    testLogging.showStandardStreams = true
    useJUnitPlatform()
}

javadoc.classpath += configurations.provided

task hadoopOfficeJavaDocs(type: Jar) {
  classifier = 'javadoc'
  from javadoc
}


task sourcesJar(type: Jar) {
    classifier = 'sources'
    from sourceSets.main.allSource
}

artifacts {
    archives hadoopOfficeJavaDocs, sourcesJar
}
