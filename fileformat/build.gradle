apply plugin: 'java'
apply plugin: 'maven-publish'
apply plugin: 'signing'
apply plugin: 'jacoco'


sourceCompatibility = 1.8
compileJava.options.encoding = 'UTF-8'

group = "com.github.zuinnote"
archivesBaseName = "hadoopoffice-fileformat"
version = "1.3.7"

jar {
    manifest {
        attributes 'Implementation-Title': 'ZuInnoTe - Hadoop Office Analytics Library - FileFormat', 'Implementation-Version': version
    }
    baseName = 'hadoopoffice-fileformat'
}

repositories {
    mavenCentral()
    mavenLocal()
}

jacocoTestReport {
    reports {
        xml.enabled true
        csv.enabled true
    }
}


configurations {
	provided
}

eclipse {

  classpath {
    plusConfigurations += [ configurations.provided ]
  }
}

sourceSets {
    main.compileClasspath += configurations.provided
    test.compileClasspath += configurations.provided
    test.runtimeClasspath += configurations.provided
}

javadoc.classpath += configurations.provided


task hadoopOfficeJavaDocs(type: Jar) {
  classifier = 'javadoc'
  from javadoc
}


task sourcesJar(type: Jar) {
    classifier = 'sources'
    from sourceSets.main.allSource
}

artifacts {
    archives hadoopOfficeJavaDocs, sourcesJar
}

test {
    testLogging.showStandardStreams = true
    useJUnitPlatform()
}

publishing {
    publications {
        mavenJava(MavenPublication) {
		groupId 'com.github.zuinnote'
            artifactId 'hadoopoffice-fileformat'
            version '1.3.7'
            from components.java
        }
    }
}


dependencies {

   // Apache POI (for Excel parsing)
   compile("org.apache.poi:poi:4.1.2")
   compile("org.apache.poi:poi-ooxml:4.1.2") 
   // for signing and verification of signatures
    provided("org.bouncycastle:bcprov-ext-jdk15on:1.62")
    provided("org.bouncycastle:bcpkix-jdk15on:1.62")
    provided("org.apache.santuario:xmlsec:2.1.2")
   // hadoop libraries
   provided("org.apache.hadoop:hadoop-common:2.7.0")
   provided("org.apache.hadoop:hadoop-mapreduce-client-common:2.7.0")
   // log4j2
   provided("org.apache.logging.log4j:log4j-api:2.11.2")
   // test
   testCompile group: 'org.junit.jupiter', name: 'junit-jupiter-api', version: '5.5.2'

   testRuntime group: 'org.junit.platform', name: 'junit-platform-launcher', version: '1.5.2'
   testRuntime group: 'org.junit.jupiter', name: 'junit-jupiter-engine', version: '5.5.2'
}
